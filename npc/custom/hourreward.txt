//===== Hourly Points Script =========================================
//===== By: ==========================================================
//= GorthexTiger modified by Nibi
//===== Current Version: =============================================
//= 1.0
//===== Compatible With: =============================================
//= Any eAthena Version
//===== Description: =================================================
//= Get Points every successful hours of gameplay, you cannot get
//= the points even if you miss a second or a minute. A player will
//= get a very big bonus if they played 12 hours consecutively
//= or without logging out of the game. If the player is vending
//= the script will then stop.
//===== Additional Comments: =========================================
//= You can modify the script to your liking.
//= The default points is Kafrapoints change it anyway if you like.
//= 1.1 = Check Chatting too
//= 1.2 = 5 Minute Idle Check & @at/@autotrade check.
//= 1.3 = Corrected the current balance line on 12 Hours Consecutive
//===== Hurtsky Modification =========================================
//= 1.4 = 7/11/2016	=	Fix get item for rathena latest rev
//					=	replace dispbottom with message
//					=	rearrage message
//					=	reset count after getting the reward
//====================================================================
-	script	hourlypoints	-1,{

//--Start of the Script
OnPCLoginEvent:
	attachnpctimer ""+strcharinfo(0)+"";
	initnpctimer;
end;

OnTimer30000:

//Check if Vending (normal or @at)
	if(checkvending() >= 1) {
		message strcharinfo(0),"[Hourly Reward]: The hourly timer stopped because you were vending. Please relog if you wish to start again.";
		stopnpctimer;
	end;
	}

//Check chatting
	if(checkchatting() == 1) {
		message strcharinfo(0),"[Hourly Reward]: The hourly timer stopped because you were in chatting ROOM. Please relog if you wish to start again.";
		stopnpctimer;
	end;
	}

//Check if Idle
	getmapxy( .@map$, .@x, .@y, 0 );
	if(@map$ == .@map$ && @x == .@x && @y == .@y) {
		set @afk, @afk + 1;
	}

//If move timer resets
	else {
		set @afk, 0;
	}
	set @map$, .@map$; set @x, .@x; set @y, .@y;

	//Idle Check for 5 Minutes
	if(@afk == 5) {
		message strcharinfo(0),"[Hourly Reward]: The hourly timer stopped because you were idle for 5 minutes. Please relog if you wish to start again.";
		stopnpctimer;
		end;
	}

end;

OnTimer60000:
	set @minute, @minute + 1;
	//Check for 1 Minute

	if(@minute == 60){
		message strcharinfo(0),"[Hourly Reward]:You received 1 box of gum for continously playing in 1 hour";
		getitem 13607,1;
		set @minute,0;
		set @consecutive_hour, @consecutive_hour + 1;
	}
	
	//Check for 12 hours consecutive
	if(@consecutive_hour == 12) {
		message strcharinfo(0),"[Hourly Reward]:You received 5 box of Yggdrasil Berry Box";
		message strcharinfo(0),"[Hourly Reward]:You received 5 pieces of Rojak Gold Coin";
		message strcharinfo(0),"[Hourly Reward]:You received 5 scrolls of Transformation Scroll(Horn Scaraba)";
		message strcharinfo(0),"[Hourly Reward]:You received 5 scrolls of Transformation Scroll(Kobold Archer)";
		message strcharinfo(0),"[Hourly Reward]:You received 1 piece of Medal of Hunter";
		message strcharinfo(0),"[Hourly Reward]:Thank you for playing 12 consecutive hours, enjoy your reward!";
		getitem 13517,5;
		getitem 50008,5;
		getitem 22750,5;
		getitem 22753,5;
		getitem 50028,1;
		set @consecutive_hour,0;
	}

stopnpctimer;

initnpctimer;

end;
}

//--End of the Script