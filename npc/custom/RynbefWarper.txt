//===== Rynbef's Script ======================================= 
//= Rynbef's SQL Warper
//===== By: =================================================== 
//= Rynbef
//===== Current Version: ======================================
//= 1.0
//===== Compatible With: ======================================
//= rAthena Project
//===== Description: ========================================== 
//= A complete sql based warper
//= Coordinates and duplicates based on Euphy's Warper
//===== Additional Comments: ==================================
//= 1.0 Initial script.
//=============================================================
//!!! -- Any change can make this script unusable. Please be careful. -- !!!\\
-	script	Warper	-1,{

query_sql ("SELECT `ID`,`Display_Name`, `MinGroupID` FROM `Warper_Categories` WHERE `State`='0'", .@CatID, .@CatName$, .@CatMinGroup);
if(.LW && LastWarp$[0] != "")set .@CatMenu$,"^777777~^000000 Last Warp [ ^007700"+LastWarp$[0]+"^000000 ]";
if(!getarraysize(.@CatID))goto L_Err;
for(set .@i,0; .@i<getarraysize(.@CatID); .@i++){
	if(getgroupid() >= .@CatMinGroup[.@i]){
		set .@CatMenu$,.@CatMenu$+":^777777~^000000 "+.@CatName$[.@i];
	} else {
		set .@CatMenu$,.@CatMenu$+":";
	}
}
set .@s,select(.@CatMenu$)-2;
if(.@s == -1 && .LW && LastWarp$[0] != ""){
	query_sql ("SELECT `Parent_ID`, `Restricted`, `X`, `Y` FROM `Warper_Submenu` WHERE `Map`='"+LastWarp$[1]+"' AND `Display_Name`='"+LastWarp$[0]+"'", .@ParentID, .@Restricted, .@X, .@Y);
	if(.@ParentID && (.@Restricted == .Restrict || .@Restricted == 0)){
		query_sql ("SELECT `Category`, `Restricted` FROM `Warper_Menu` WHERE `ID`='"+.@ParentID+"'", .@CatID, .@Restricted);
		query_sql ("SELECT `MinGroupID`,`Display_Name` FROM `Warper_Categories` WHERE `ID`='"+.@CatID+"' AND `State`='0'", .@MinGroupID, .@DisplayName$);
		if((.@Restricted == .Restrict || .@Restricted == 0) && getgroupid() >= .@MinGroupID && .@DisplayName$ != ""){
			warp LastWarp$[1],.@X,.@Y;
			end;
		} else {
			mes .n$;
			mes "The map is disabled or you are not allowed to warp to this map.";
			close;
		}
	} else if(.@ParentID && (.@Restricted != .Restrict && .@Restricted != 0)) {
		mes .n$;
		mes "The map is disabled or you are not allowed to warp to this map.";
		close;
	} else {
		query_sql ("SELECT `Category`, `Restricted`, `X`, `Y` FROM `Warper_Menu` WHERE `Map`='"+LastWarp$[1]+"' AND `Display_Name`='"+LastWarp$[0]+"'", .@CatID, .@Restricted, .@X, .@Y);
		query_sql ("SELECT `Display_Name`, `MinGroupID` FROM `Warper_Categories` WHERE `ID`='"+.@CatID+"' AND `State`='0'", .@DisplayName$, .@MinGroupID);
		if ((.@Restricted == .Restrict || .@Restricted == 0) && getgroupid() >= .@MinGroupID && .@DisplayName$ != ""){
			warp LastWarp$[1],.@X,.@Y;
			end;
		} else {
			mes .n$;
			mes "The map is disabled or you are not allowed to warp to this map.";
			close;
		}
	}
}
query_sql ("SELECT `ID`,`Display_Name`, `Map`, `X`, `Y` FROM `Warper_Menu` WHERE `Category`='"+.@CatID[.@s]+"' AND (`Restricted`='"+.Restrict+"' OR `Restricted`='0')", .@MenuID, .@MenuName$, .@MenuMap$, .@MenuX, .@MenuY);
if(!getarraysize(.@MenuID))goto L_Err;
for(set .@i,0; .@i<getarraysize(.@MenuID); .@i++){
	set .@Menu$,.@Menu$+":"+.@MenuName$[.@i];
}
set .@s,select(.@Menu$)-2;
query_sql ("SELECT `ID`,`Display_Name`, `Map`, `X`, `Y` FROM `Warper_Submenu` WHERE `Parent_ID`='"+.@MenuID[.@s]+"' AND (`Restricted`='"+.Restrict+"' OR `Restricted`='0')", .@SubID, .@SubName$, .@SubMap$, .@SubX, .@SubY);
if(!getarraysize(.@SubID)){
	if(.@MenuMap$[.@s] == "")goto L_Err02;
	setarray LastWarp$[0],.@MenuName$[.@s],.@MenuMap$[.@s];
	warp .@MenuMap$[.@s],.@MenuX[.@s],.@MenuY[.@s];
	end;
} else {
	for(set .@i,0; .@i<getarraysize(.@SubID); .@i++){
		set .@SubMenu$,.@SubMenu$+":"+.@SubName$[.@i];
	}
	set .@s,select(.@SubMenu$)-2;
	setarray LastWarp$[0],.@SubName$[.@s],.@SubMap$[.@s];
	warp .@SubMap$[.@s],.@SubX[.@s],.@SubY[.@s];
	end;
}
end;

L_Err:
	mes .n$;
	mes "^FF0000Error:^000000 no entries found. Please contact a game master.";
	close;

L_Err02:
	mes .n$;
	mes "^FF0000Error:^000000 this map isnt completly configured yet.";
	close;

OnInit:
	set .n$,"[^007700Warper^000000]";
	set .LW,1; //0=disable last warp function,1=enable last warp function  --player cant bypass changed configurations--
	set .Restrict,1; //1=If server is renewal,2=If server is pre-renewal
	end;
}
// --------------------------------------------------
//	Duplicates:
// --------------------------------------------------
alb2trea,57,70,6	duplicate(Warper)	Warper#tre	811
alberta,28,240,4	duplicate(Warper)	Warper#alb	811
aldebaran,145,118,4	duplicate(Warper)	Warper#alde	811
amatsu,203,87,4	duplicate(Warper)	Warper#ama	811
ayothaya,209,169,6	duplicate(Warper)	Warper#ayo	811
comodo,194,158,4	duplicate(Warper)	Warper#com	811
einbech,59,38,6	duplicate(Warper)	Warper#einbe	811
einbroch,69,202,4	duplicate(Warper)	Warper#einbr	811
gef_fild10,71,339,4	duplicate(Warper)	Warper#orc	811
geffen,124,72,4	duplicate(Warper)	Warper#gef	811
glast_01,372,308,4	duplicate(Warper)	Warper#glh	811
gonryun,162,122,4	duplicate(Warper)	Warper#gon	811
hugel,101,151,4	duplicate(Warper)	Warper#hug	811
izlu2dun,110,92,4	duplicate(Warper)	Warper#izd	811

izlude,134,150,4	duplicate(Warper)	Warper#izl	811
//PRE-RE: izlude,132,120,4	duplicate(Warper)	Warper#izl	811

jawaii,253,138,4	duplicate(Warper)	Warper#jaw	811
lighthalzen,162,102,4	duplicate(Warper)	Warper#lhz	811
louyang,208,103,6	duplicate(Warper)	Warper#lou	811
manuk,274,146,6	duplicate(Warper)	Warper#man	811
mid_camp,216,288,4	duplicate(Warper)	Warper#mid	811
mjolnir_02,85,364,4	duplicate(Warper)	Warper#mjo	811
moc_ruins,64,164,4	duplicate(Warper)	Warper#moc	811
morocc,159,97,4	duplicate(Warper)	Warper#mor	811
moscovia,229,191,4	duplicate(Warper)	Warper#mos	811
nameless_n,259,213,4	duplicate(Warper)	Warper#nam	811
niflheim,205,179,4	duplicate(Warper)	Warper#nif	811
pay_arche,42,134,4	duplicate(Warper)	Warper#arc	811
payon,182,108,4	duplicate(Warper)	Warper#pay	811
prontera,159,192,4	duplicate(Warper)	Warper#prt	811
prt_fild05,279,223,6	duplicate(Warper)	Warper#cul	811
rachel,135,116,4	duplicate(Warper)	Warper#rac	811
splendide,205,153,4	duplicate(Warper)	Warper#spl	811
thor_camp,249,76,4	duplicate(Warper)	Warper#thor	811
umbala,106,150,3	duplicate(Warper)	Warper#umb	811
veins,214,123,4	duplicate(Warper)	Warper#ve	811
xmas,150,136,6	duplicate(Warper)	Warper#xmas	811
yuno,162,47,4	duplicate(Warper)	Warper#yuno	811

// --------------------------------------------------
//	Duplicates (Renewal):
// --------------------------------------------------
brasilis,201,222,4	duplicate(Warper)	Warper#bra	811
dewata,204,186,6	duplicate(Warper)	Warper#dew	811
dicastes01,194,194,6	duplicate(Warper)	Warper#dic	811
ecl_in01,51,60,4	duplicate(Warper)	Warper#ecl	811
malangdo,134,117,6	duplicate(Warper)	Warper#mal	811
malaya,231,204,4	duplicate(Warper)	Warper#ma	811
mora,57,152,4	duplicate(Warper) Warper#mora 811